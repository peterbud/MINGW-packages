# Maintainer: Peter Budai <peterbudai@hotmail.com>

_realname=mathjax
pkgbase=mingw-w64-${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.7.4
pkgrel=1
pkgdesc='An open source JavaScript display engine for mathematics that works in all modern browsers'
url='https://www.mathjax.org/'
arch=(any)
license=(Apache)
depends=(${MINGW_PACKAGE_PREFIX}-fontconfig)
source=("$pkgname-$pkgver.tar.gz::https://github.com/mathjax/MathJax/archive/$pkgver.tar.gz")
sha256sums=('c7cf584b6a6a7b33b4900f125bc1f413cb047c704f3695fc88219e2a7f598021')

prepare() {
  cd ${_realname}-$pkgver

  # Remove unneeded stuff, see https://github.com/mathjax/MathJax-docs/wiki/Guide%3A-reducing-size-of-a-mathjax-installation
  rm -r docs test unpacked
  rm -r fonts/HTML-CSS/TeX/png
  for _format in eot otf svg ; do
    find . -type d -name "$_format" -prune -exec rm -rf {} \;
  done
}

package() {
  mkdir -p "$pkgdir/${MINGW_PREFIX}/share/licenses/mathjax"
  cp -a MathJax-$pkgver "$pkgdir/${MINGW_PREFIX}/share/mathjax"
  mv "$pkgdir/${MINGW_PREFIX}/share/mathjax/LICENSE" "$pkgdir/${MINGW_PREFIX}/share/licenses/mathjax/LICENSE"
}
